allprojects {
    group 'com.endoflineblog.truffle'
    version '1.0'
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    // These arguments are required because of the JVM 11's module system -
    // we grant this module access to the packages from the Truffle API JAR
    test {
        jvmArgs '-ea',
                '--add-exports',
                    'org.graalvm.truffle/com.oracle.truffle.api=ALL-UNNAMED',
                '--add-exports',
                    'org.graalvm.truffle/com.oracle.truffle.api.nodes=ALL-UNNAMED',
                '--add-exports',
                    'org.graalvm.truffle/com.oracle.truffle.api.dsl=ALL-UNNAMED'

        testLogging {
            events 'failed'
            exceptionFormat 'full'
        }
    }

    ext {
        graal_version = '21.3.1'
        antlr_version = '4.9.2'
    }

    dependencies {
        implementation "org.graalvm.truffle:truffle-api:$graal_version"
        annotationProcessor "org.graalvm.truffle:truffle-dsl-processor:$graal_version"

        testImplementation "junit:junit:4.12"
    }
}
